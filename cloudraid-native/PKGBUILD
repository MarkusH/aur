# Maintainer: Markus Holtermann <aur@markusholtermann.eu>
# Contributor: Florian Bausch <studium@fbausch.de>
# Contributor: Markus Holtermann <aur@markusholtermann.eu>
pkgname=cloudraid-native
pkgver=0.1.1
pkgrel=1
pkgdesc='The native backend to CloudRAID'
arch=('i686' 'x86_64')
url='https://github.com/Markush2010/CloudRAID/'
license=('Apache')
depends=('glibc')
makedepends=('java-environment')
source=("https://github.com/downloads/Markush2010/CloudRAID/$pkgname-$pkgver.tar.gz")
md5sums=('cbef25b6b2226e36e639c01e996c5200')

build() {
    cd "$srcdir/$pkgname-$pkgver/src/native"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver/src/native"
    make test
    ./test/test_sha256
    ./test/test_raid5
    ./test_raid5.sh
    rm ./*.dat
}

package() {
    cd "$srcdir/$pkgname-$pkgver/src/native"
    install -d $pkgdir/usr/lib/
    make DESTDIR=$pkgdir install
}
